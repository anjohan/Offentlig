set terminal epslatex size 6.5,6 color colortext

set format x "$\\num{%g}$"
set format y "$\\num{%g}$"

set key bottom right
set xlabel "$T$"
set key autotitle columnheader

set style line 1 lw 5 lc rgb "blue"
set style line 5 lw 5 lc rgb "red"
set style line 9 lw 5 lc rgb "green"
set style line 13 lw 5 lc rgb "cyan"

set output outfile
set xtics 0.05

set multiplot layout 2,2 scale 1,1

set ylabel "$\\langle E\\rangle/L^2$"
set title "\\textbf{Energy}"
plot for [i=2:17:4] infile using 1:i with lines ls i-1

set key top right
set ylabel "$C_V/L^2$"
set title "\\textbf{Heat capacity}"
plot for [i=3:17:4] infile using 1:i with lines ls i-2

set ylabel "$\\langle\\abs{M}\\rangle/L^2$"
set title "\\textbf{Magnetisation}"
plot for [i=4:17:4] infile using 1:i with lines ls i-3

unset yrange
set ylabel "$\\chi/L^2$"
set title "\\textbf{Magnetic susceptibility}"
plot for [i=5:17:4] infile using 1:i with lines ls i-4

unset multiplot
