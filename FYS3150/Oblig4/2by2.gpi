set terminal epslatex size 6.5,6 color colortext

set format x "$\\num{%g}$"
set format y "$\\num{%g}$"

set xtics 50000

set key center right

set style line 1 lw 5 lc rgb "blue"
set style line 2 lw 5 lc rgb "red"

set output "2by2.plt"

set xlabel "Iteration cycles"

set multiplot layout 2,2 scale 1,1

set ylabel "$E$"

set yrange[-9:0]

set title "\\textbf{Energy}"

plot "2by2.dat" using 1:3 title "\\textbf{$T=1$}" with lines ls 1, \
"2by2hot.dat" using 1:3 title "\\textbf{$T=2$}" with lines ls 2

set ylabel "$C_V$"

set yrange[-1:16]

set title "\\textbf{Heat capacity}"

plot "2by2.dat" using 1:8 title "\\textbf{$T=1$}" with lines ls 1, \
"2by2hot.dat" using 1:8 title "\\textbf{$T=2$}" with lines ls 2

set ylabel "$M$"

set yrange[-4:4]

set title "\\textbf{Magnetic moment}"

plot "2by2.dat" using 1:5 title "\\textbf{$T=1$}" with lines ls 1, \
"2by2hot.dat" using 1:5 title "\\textbf{$T=2$}" with lines ls 2

set ylabel "$\\chi$"

set yrange[0:17]

set title "\\textbf{Magnetic susceptibility}"

plot "2by2.dat" using 1:9 title "\\textbf{$T=1$}" with lines ls 1, \
"2by2hot.dat" using 1:9 title "\\textbf{$T=2$}" with lines ls 2


unset multiplot
